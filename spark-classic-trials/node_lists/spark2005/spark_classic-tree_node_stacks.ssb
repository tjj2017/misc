--  This file is a shadow of the generic package body of
--  SPARK_Classic.Bounded_Stacks instantiated with an Element_Type
--  of Node_Id_Trees.Tree_Node.
package body SPARK_Classic.Tree_Node_Stacks is
   --# accept W, 3, "pragma Inline has no semantic effect";

   --  A null procedure to define the relationship between
   --  S.Count and Count (S).
   procedure Refined_Count (S : Stack)
   --# derives null from S;
   --# post for all C in Stack_Count => ((S.Count = C) = (Count (S) = C));
   is
      --# hide Refined_Count;
   begin
      null;
   end Refined_Count;

   --  A null procedure to define the relationship between
   --  S.Count and Eempty (S).
   procedure Refined_Empty (S : Stack)
   --# derives null from S;
   --# post Is_Empty (S) = (S.Count = 0);
   is
      --# hide Refined_Empty;
   begin
      null;
   end Refined_Empty;

   -----------
   -- Count --
   -----------

   function Count (S : Stack) return Stack_Count
   --# return S.Count;
   is
   begin
      return S.Count;
   end Count;
   pragma Inline (Count);

    --------------
   -- Is_Empty --
   --------------

   function Is_Empty
     (S : Stack)
      return Boolean
   is
   begin
      Refined_Count (S);
      return S.Count = 0;
   end Is_Empty;
   pragma Inline (Is_Empty);

   ---------------
   -- New_Stack --
   ---------------

   procedure New_Stack
     (S : out Stack)
   is
   begin
      S.Count := 0;
      --# accept Flow, 23, S.Contents, "Setting Count to zero means no contents" &
      --#        Flow, 32, S.Contents, "As above" &
      --#        Flow, 31, S.Contents, "As above" &
      --#        Flow, 602, S, S.Contents, "As above";
      Refined_Empty (S);
   end New_Stack;
   pragma Inline (New_Stack);

   ----------
   -- Push --
   ----------

   procedure Push
     (S : in out Stack;
      Value : Node_Id_Trees.Tree_Node)
   is
   begin
      S.Count := S.Count + 1;
      S.Contents (S.Count) := Value;
   end Push;
   pragma Inline (Push);

   ---------
   -- Pop --
   ---------

   procedure Pop
     (S : in out Stack;
      Value : out Node_Id_Trees.Tree_Node)
   is
   begin
      Value := S.Contents (S.Count);
      S.Count := S.Count - 1;
      --# check S.Count = Count (S~) - 1;
   end Pop;
   pragma Inline (Pop);

   ---------
   -- Top --
   ---------

   function Top
     (S : Stack)
      return Node_Id_Trees.Tree_Node
   is
   begin
      return S.Contents (S.Count);
   end Top;
   pragma Inline (Top);

   -----------------
   -- Predecessor --
   -----------------

   function Predecessor
     (S : Stack;
      Pred_Num : Stack_Count)
      return Node_Id_Trees.Tree_Node
   is
   begin
      return S.Contents (S.Count - Pred_Num);
   end Predecessor;
   pragma Inline (Predecessor);

   -----------
   -- Clear --
   -----------

   procedure Clear
     (S : out Stack)
   is
   begin
      S.Count := 0;
      --# accept Flow, 23, S.Contents, "Setting Count to zero means no contents" &
      --#        Flow, 31, S.Contents, "As above" &
      --#        Flow, 32, S.Contents, "As above" &
      --#        Flow, 602, S, S.Contents, "As above";
      Refined_Empty (S);
    end Clear;
   pragma Inline (Clear);

end SPARK_Classic.Tree_Node_Stacks;
