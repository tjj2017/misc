with SPARK_Classic.Expose_Nodes;
--# inherit SPARK_Classic.Expose_Nodes;
package SPARK_Classic.Bounded_Stacks is
   subtype Element_Type is Expose_Nodes.Tree_Node;
      
   Stack_Size : constant Positive := 32;
   
   type Stack is tagged private;

   subtype Stack_Count is Natural range 0 .. Stack_Size;

   function Count    (S : Stack) return Stack_Count;
   function Is_Empty (S : Stack) return Boolean;
   --# return Is_Empty (S) <-> Count (S) = 0;

   procedure New_Stack (S : out Stack);
   --# post Is_Empty (S);

   procedure Push (S : in out Stack;
                   Value : Element_Type);
   --# pre  Count (S) < Stack_Size;
   --# post not Is_Empty (S) and 
   --#     Count (S) = Count (S~) + 1;

   procedure Pop  (S : in out Stack;
                   Value : out Element_Type);
   --# pre not Is_Empty (S);
   --# post Count (S) = Count (S~) - 1;

   function Top (S : Stack) return Element_Type;
   --# pre  not Is_Empty (S);

   function Predecessor (S : Stack;
                         Pred_Num : Stack_Count)
                         return Element_Type;
   --# pre Pred_Num < Count (S);

   procedure Clear (S : out Stack);
   --# post Is_Empty (S);
private
   subtype Stack_Index is Stack_Count range 1 .. Stack_Count'Last;
   type Stack_Contents is array (Stack_Index) of Element_Type;

   type Stack is tagged
      record
         Count    : Stack_Count;
         Contents : Stack_Contents;
      end record;
      
end SPARK_Classic.Bounded_Stacks;
