with SPARK_Classic.Expose_Nodes;
use type SPARK_Classic.Expose_Nodes.Tree_Node;
--# inherit SPARK_Classic.Expose_Nodes;
package SPARK_Classic.Dynamic_Tables is
   subtype Table_Index_Type is Expose_Nodes.Tree_Node;
   subtype Table_Component_Type is Expose_Nodes.Actual_Node;
   type Table_Type is private;

   Table_Low_Bound : constant Table_Index_Type := Table_Index_Type'First;
   subtype Valid_Table_Index_Type is Table_Index_Type
     range Table_Low_Bound + 1 .. Table_Index_Type'Last;
   subtype Table_Last_Type is Table_Index_Type
     range Table_Low_Bound .. Table_Index_Type'Last;

   function Last_Index (T : Table_Type) return Table_Last_Type;
   --  Returns the current value of the last used entry in the table, which can
   --  then be used as a subscript for Table.

   function Is_Empty (T : Table_Type) return Boolean;
   --# return E => (E -> Last_Index (T) = 0);

   procedure Init (T : out Table_Type);
   --# post Last_Index (T) = Table_Last_Type'First;

   procedure Append (T : in out Table_Type; New_Val : Table_Component_Type);
   --# post (Last_Index (T) in Valid_Table_Index_Type) and
   --#      (Last_Index (T) = Last_Index (T~) + 1) and 
   --#      (Last_Index (T) < Table_Last_Type'Last); 
   --  Appends New_Val onto the end of the table
   --  Equivalent to:
   --    Increment_Last_Index (T);
   --    T.Table (T.Last_Index) := New_Val;

   procedure Set_Item
     (T     : in out Table_Type;
      Index : Valid_Table_Index_Type;
      Item  : Table_Component_Type);
   --  Put Item in the table at position Index. If Index points to an existing
   --  item (i.e. it is in the range First .. Last_Index (T)), the item is
   --  replaced. Otherwise (i.e. Index > Last_Index (T)), the table is
   --  expanded, and Last_Index is set to Index.

   function Get_Item (T : Table_Type; Index : Valid_Table_Index_Type)
                      return Table_Component_Type;

   procedure Set_Last (T : in out Table_Type; New_Val : Table_Last_Type);
   --# post Last_Index (T) = New_Val;
   --  This procedure sets Last_Index to the indicated value. If necessary the
   --  table is reallocated to accommodate the new value (i.e. on return the
   --  allocated table has an upper bound of at least Last). If Set_Last
   --  reduces the size of the table, then logically entries are removed from
   --  the table. If Set_Last increases the size of the table, then new entries
   --  are logically added to the table.

private
--# hide SPARK_Classic.Dynamic_Tables;

   Table_Initial     : Positive := 8;
   Table_Increment   : Natural := 100;
   Release_Threshold : Natural := 0; -- size in bytes

   package Gnat_Table is new GNAT.Dynamic_Tables
     (Table_Component_Type => Table_Component_Type,
      Table_Index_Type     => Table_Index_Type,
      Table_Low_Bound      => Table_Low_Bound,
      Table_Initial        => Table_Initial,
      Table_Increment      => Table_Increment,
      Release_Threshold    => Release_Threshold);

   type Table_Type is tagged record
      The_Table : Gnat_Table.Instance;
   end record;
end SPARK_Classic.Dynamic_Tables;
